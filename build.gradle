apply plugin: 'idea'

import java.awt.Desktop

buildscript {
    repositories {
        mavenLocal()
    }

    dependencies {
        classpath 'at.dinauer.reporting:dependency-reporting-plugin:0.1-SNAPSHOT'
    }
}

def dependencyReportDir = "$buildDir/report/dependencies"

task createDependencyGraph(type: CreateDependencyGraph) {
	dependencyGatherer = new MavenDependencyGatherer(rootPomFile: new File(rootDirOfProjectToSearch + '/pom.xml'), dependencyRegex: dependencyRegex, projectRegex: projectRegex)
	translator = new DependencyToDotLanguageTranslator()
	fileCreator = new GraphvizDotfileCreator()
	regex = dependencyRegex

	graphDotFile = file("$dependencyReportDir/dependencies.gv")

	outputs.upToDateWhen { false }
}

task copyDependencyReportLibraries(type: Copy) {
	from "src/main/lib"
	into dependencyReportDir
}

task copyDependencyReportTemplate(dependsOn: [createDependencyGraph, copyDependencyReportLibraries]) << {
	copy {
		from "src/main/resources"
		into dependencyReportDir

		String graphAsDot = createDependencyGraph.outputs.files.singleFile.text

		expand(dependencies: graphAsDot)
	}
}

task createDependencyReport(dependsOn: copyDependencyReportTemplate)

task openDependencyReport(mustRunAfter: createDependencyReport) << {
	Desktop.getDesktop().open(new File("$dependencyReportDir/dependencies.html"))
}

class CreateDependencyGraph extends DefaultTask {
	DependencyGatherer dependencyGatherer
	DependencyToDotLanguageTranslator translator
	GraphvizDotfileCreator fileCreator
	String regex

	@OutputFile
	File graphDotFile

	@TaskAction
	void createDotfile() {
		def dependenciesGroupedByModule = dependencyGatherer.gatherDependencies()

		def graphEntries = translator.translateDependenciesToDotLanguage(dependenciesGroupedByModule, regex)

		fileCreator.createDirectedGraphFile(graphDotFile, graphEntries)
	}
}

interface DependencyGatherer {
	Map<String, Set<String>> gatherDependencies()
}

class MavenDependencyGatherer implements DependencyGatherer {
	File rootPomFile
	String dependencyRegex
	String projectRegex

	Map<String, Set<String>> gatherDependencies() {
		new MavenMultiModulePomParser().parseMatchingDependencies(rootPomFile, dependencyRegex, projectRegex)
	}
}